# This module is responsible for editing the answers given by respondents to a survey questions. To work properly two parameters must be given: ID of an respondent and ID of the survey. Then the information about the survey and current respondent answers are downloaded from database and displayed in a form. 

# Variables used in database operations.

var db = database "surveybuilder";

var readysurveys =
    table "surveys"
    with (id : Int,
	  name : String,
	  textareasno : Int
    	  ) from db;

var textareas =
    table "textareas"
    with (
      id : Int,
	  surveyid : Int,
	  questioncontent : String
    	  ) from db;

var textareaentries =
	table "textareaentries"
	with
	(
	id : Int,
	textareaid : Int,
	respondendid : Int,
	answer : String
	) from db;

var readyrespondents =
	table "surveyrespondents"
	with
	(
	id : Int,
	surveyid : Int,
	name : String
	) from db;


var readycheckboxquestions = 
    table "checkboxquestions"
    with (
    id : Int,
    surveyid : Int,
    checkboxcontent : String
    ) from db;

var readycheckboxoptions = 
    table "checkboxoptions"
    with (
    id : Int,
    questionid : Int,
    optioncontent : String
    ) from db;

var readycheckboxdbentries =
    table "checkboxentries"
    with (
    id : Int,
    optionid : Int,
    respondendid : Int,
    optionvalue : Int
    ) from db;

var checkboxdbentries =
    table "checkboxentries"
    with (
    optionid : Int,
    respondendid : Int,
    optionvalue : Int
    ) from db;


# This function id called after user clicks on 'Next' button and submits his/her answers. It prints out his/her decisions and enables asks if he/she is 100% sure of this answers. It is kind of bridge between writing/selecting answers to all the questions and storing them in a database.
fun editSummarize(inputData, surveyId, surveyName, respondentId)
{

var editFormlet = formlet 
				<#>
                    {
                    for(cbentry <- inputData.cbvalues)
                    {
                    <p><b>{stringToXml(cbentry.questiontxt)}</b>
                    <table>
                    {for(singleOption <- cbentry.answers)
                    {
                    if(singleOption.selv)
                    {
                    <tr><td><font color="green">{stringToXml(singleOption.otxt)}</font></td></tr>
                    }
                    else
                    {
                    <tr><td><font color="red">{stringToXml(singleOption.otxt)}</font></td></tr>
                    }
                    }
                    }
                    </table>
                    </p>
                    }
                    }
        			{
         			for(entry <- inputData.listofvalues)
         			{
         			<p><b>{stringToXml(entry.question)}</b><br></br><em>{stringToXml(entry.answer)}</em></p>
         			}
         			}
         			<p>If they are in line with your opinion please submit them by clicking on 'confirm' button. Otherwise go back.</p>
         			<p>{submitButton("confirm") -> submit}</p> 
               	</#> 
        yields { 
        (id = surveyId , data = inputData)
        };

page
<html> 
 <title>SingleSurveyEditor</title>
 <body bgcolor="#ffffff">    

 <h1>{stringToXml(surveyName)}</h1>
 
<p>That is how do your answers look like now:</p>

 {
 	editFormlet => fun (par) { confirmEdit(par, surveyName, respondentId) }
 }


 <hr />
   
</body>				  
</html>
}

# Updates an answer for a given question for a given respondent.
fun updatesinglerowTA(r, respondentId)
{
	update (x <-- textareaentries)
	where (x.textareaid == r.id && x.respondendid == respondentId)
	set (
	 answer = r.answer
	);
	()
}

# Updates an answer for a given checkbox question option for a given respondent.
fun updateSingleCbEntry(optionId, respondentIt, checkedornot)
{
    var asd = if(checkedornot)
    {
    update (x <-- checkboxdbentries)
    where (x.optionid == optionId && x.respondendid == respondentIt)
    set (
     optionvalue = 1
    )
    }
    else
    {
    update (x <-- checkboxdbentries)
    where (x.optionid == optionId && x.respondendid == respondentIt)
    set (
     optionvalue = 0
    )
    };
    ()
}

# Calls all the update functions and in the end displays information about the success.
fun confirmEdit(inputValues, sName, respondentId)
{

var data = inputValues.data.listofvalues;
var someMagic = for(r <- data)
{
	updatesinglerowTA(r, respondentId);
	[]
};

var cbdata = inputValues.data.cbvalues;
var nameforfor = for (singleQuestion <- cbdata)
{
    for (singleOpt <- singleQuestion.answers)
    {
        updateSingleCbEntry(singleOpt.oid,respondentId,singleOpt.selv);
        []
    }
    
};


page
<html> 
 <title>SingleSurveyEditor</title>
 <body bgcolor="#ffffff">    

 <h1>{stringToXml(sName)}</h1>
 
 <p>Your answers have been successfully updated in our database !</p>

 <br></br>
 <img src="success.jpg" alt="" width="620" height="349"></img>
 
   
</body>				  
</html>

}

# Recursive function responsible for presenting answer options for the questions of a checkbox type.
fun checkboxesFormlet(cboptions)
{
      
      if (cboptions == [])
                    formlet <#></#> yields []
                else {
                    var cbopt = hd(cboptions).qco;
                    formlet <#>
                        <tr>
                            <td>{ stringToXml(hd(cboptions).qotxt) }</td>
                            <td> { checkboxDefault(cbopt) -> c } </td>
                        </tr>
                        { checkboxesFormlet(tl(cboptions)) -> rest }                    
                    </#>
                    yields
                        ("otxt"=hd(cboptions).qotxt,"oid"=hd(cboptions).qoid,"selv"=c)::rest
                }
}

# Recursive function generating form to display survey questions and then gather information from user input.
fun surveyFormletCb(opt, questions, answers, checkboxes)
{
    if (opt == 1)
    {   
        if(checkboxes == [])
        {
            formlet <#>
            { surveyFormletCb(2,questions, answers,[]) -> rest }  
            </#>
            yields {
            rest
        }
        }
        else
        {
            formlet <#>
            <tr><td><b>{stringToXml(hd(checkboxes).qname)}</b></td></tr>
            <tr class="blank_row"><td colspan="5"></td></tr>
            { checkboxesFormlet(hd(checkboxes).qo) -> useranswers} 
            <tr class="blank_row"><td colspan="5"></td></tr>   
            <tr class="blank_row"><td colspan="5"></td></tr>   
            <tr class="blank_row"><td colspan="5"></td></tr>   
            { surveyFormletCb(opt,questions,answers,tl(checkboxes)) -> rest }

            </#>
            yields {
            (listofvalues = rest.listofvalues, cbvalues = (questiontxt = hd(checkboxes).qname, answers = useranswers)::rest.cbvalues)
        }
        }

    }
    else
    {
        if (questions == [])
        formlet <#>
                    <tr class="blank_row"><td colspan="5"></td></tr>
                    <tr class="blank_row"><td colspan="5"></td></tr>            
                    <tr><td></td></tr>
                    <tr><td>{submitButton("Next") -> submit}</td></tr> 
                </#> 
        yields { 
        (listofvalues = [], cbvalues = [])
        }
    else {
        formlet <#>
                 <tr>
                     <td> <b> {stringToXml(hd(questions).questioncontent)} </b> </td>
                 </tr>
                 <tr>
                    <td> {textareacss(hd(answers).answer,"width: 340px; height: 85px;") -> lanswer} </td>
                 </tr>
                 { surveyFormletCb(opt,tl(questions),tl(answers),checkboxes) -> rest }      
             </#>
        yields
        (listofvalues = (id = hd(questions).id, answer = lanswer, question = hd(questions).questioncontent)::rest.listofvalues, cbvalues = [])   
        }
    }
}

fun getSurvey(surveyselected)
{
	if (length(surveyselected) > 0){
    var selectedsurveyid = hd(surveyselected);
    for (s <-- readysurveys)
    where (s.id == stringToInt(selectedsurveyid))
    [("id"=s.id, "name"=s.name, "textareasno"=s.textareasno)]
    }
    else
    {
    []
    }
}

fun getRespondent(respondentselected, selsurveyId)
{
	if (length(respondentselected) > 0)
	{
	var selectedrespondentid = hd(respondentselected);
	for (r <-- readyrespondents)
  	where (r.id == stringToInt(selectedrespondentid) && r.surveyid == selsurveyId)
    [("id"=r.id, "surveyid"=r.surveyid, "name"=r.name)]
	}
	else
	{
	[]
	}
}

fun getCbQuestions(surveyId)
{
    for(cbquestion <- asList(readycheckboxquestions))
    where(cbquestion.surveyid == surveyId)
    [cbquestion]
}

fun getOptionsForQuestion(questionId, respondentId)
{
    for(cboption <- asList(readycheckboxoptions))
    where(cboption.questionid == questionId)
    [("qoid"=cboption.id, "qotxt"=cboption.optioncontent, "qco"=getCurrentAnswerForOption(cboption.id, respondentId))]
}

fun getCurrentAnswerForOption(optionId, respondentId)
{
    var listWithOneElement = for (cbentry <- asList(readycheckboxdbentries))
    where(cbentry.optionid == optionId && cbentry.respondendid == respondentId)
    [("optvalue" = cbentry.optionvalue)];
    var currentOptionEntry = hd(listWithOneElement).optvalue;
    if(currentOptionEntry == 1)
    {
    true
    }
    else
    {
    false
    }
}

fun prepareCheckboxes(surveyId, respondentId)
{
    
    if(surveyId <> 0)
    {
    var cbQuestions = getCbQuestions(surveyId);
    var outArray = for (singleQuestion <- cbQuestions)
    {
    var qo = getOptionsForQuestion(singleQuestion.id, respondentId);
    [("qname"=singleQuestion.checkboxcontent,"qo"=qo)]
    };
    outArray
    }
    else
    {
    []
    }
}

fun main() {

var env = environment();
var srvopt = for ((x,y) <- env) where (x == "surveyId") [y]; 
var rspndntopt = for ((x,y) <- env) where (x == "respondentId") [y];
var srvnLst = getSurvey(srvopt);
var selectedsrv = if (length(srvnLst) > 0)
 {
 	hd(srvnLst)
 }
 else
 {
 	("id"=0, "name"="takiegoimienianiktnienada", "textareasno"=0)
 }; 
var rspndntLst = getRespondent(rspndntopt,selectedsrv.id);
var selectedrspndnt = if (length(rspndntLst) > 0)
{
	hd(rspndntLst)
}
else
{
	("id"=0, "surveyid"=0, "name"="atakiegotojuznapewnonikt")
};

if (selectedrspndnt.id <> 0)
{
var surveyquestions = query { for (rquestion <-- textareas) where (rquestion.surveyid == selectedsrv.id) [rquestion] };
var surveyanswers = query { for (ranswer <-- textareaentries) where (ranswer.respondendid == selectedrspndnt.id) [ranswer] };
var checkboxes = prepareCheckboxes(selectedsrv.id, selectedrspndnt.id);
var startValue = 1;

var ep = page <table> { ( surveyFormletCb(startValue,surveyquestions, surveyanswers, checkboxes) ) => fun (r) {editSummarize(r,selectedsrv.id,selectedsrv.name,selectedrspndnt.id)} } </table>;

page
<html> 
 <title>SingleSurveyEditor</title>
 <body bgcolor="#ffffff">    

 <h1>{stringToXml(selectedsrv.name)}</h1>
  <p>Below there is placed a form of the survey so that you can improve your answers. We are glad of your engagement.</p>

{|ep|}

<hr />

</body>				  
</html>

}
else
{
	
page
<html> 
 <title>SingleSurveyEditor</title>
 <body bgcolor="#ffffff">    

 <h1>Welcome to the single survey editor of the SurveyBuilder application !</h1>
 <p>Please provide a proper both survey ID and respondent ID as an HTML parameters. To do that you should have in your URL something like that: "survey-edit.links?surveyId=<em>yourSurveyId</em>&amp;respondentId=<em>yourRespondentId</em>".</p>
 <p> If you don't know what I'm talking about, then you haven't been asked to edit this survey.</p>
   
</body>				  
</html>

}
}

main()