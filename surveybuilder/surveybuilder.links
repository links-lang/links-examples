# This file is the core of the whole "SurveyBuilder" survey management system. It is the first file to be run and from this level all other files are called depending what the user wants to do. In contains a short description of an application. Moreover there is a table with all surveys created by the user by the time and links to the modules responsible for different actions upon the surveys as well as reference to SurveyGenerator module.


# Variables used in database operations.

var db = database "surveybuilder";

var surveys =
    table "surveys"
    with (
      id : Int,
	  name : String,
	  textareasno : Int
    	  ) from db;

var surveyrespondents =
	table "surveyrespondents"
	with (
	id : Int,
	surveyid : Int,
	name : String
	) from db;

fun getNumberOfRespondents(surveyId)
{
	var respondents = for (x <- asList(surveyrespondents))
	where (x.surveyid == surveyId)
	[x];
	var result = length(respondents);
	result
}

fun generateSurveyResultsAnalyzerLink(surveyId)
{
	var link = "survey-results.links?surveyId="^^intToString(surveyId);
	link
}

fun generateSurveyFillerLink(surveyId)
{
	var link = "survey-filler.links?surveyId="^^intToString(surveyId);
	link
}

fun generateSurveyDestroyerLink(surveyId)
{
	var link = "surveydestroyer.links?surveyId="^^intToString(surveyId);
	link
}

fun generateSurveyRespondentsLink(surveyId)
{
	var link = "survey-respondents.links?surveyId="^^intToString(surveyId);
	link
}

fun main() {

page
<html> 
 <title>SurveyBuilder</title>
 <body bgcolor="#ffffff">    

 <h1>SurveyBuilder</h1>

<p>Welcome to the <b>SurveyBuilder</b> application! This small survey management system consists of 4 modules which will help you to cope with your surveys.</p>
<p>The modules include:
<ul>
<li>SurveyGenerator - module responsible for generation a single survey. User types the questions he/she wants to include in his/her survey and chooses for each question its type (open questions, questions with some user-predefined answers). Moreover user should provide the name of the survey which will be generated and also the time needed to fill out the survey along with some welcome text and a link where respondent may go after filling out the survey.</li>
<li>SurveyFiller - module which is sent to the respondent so that he/she could fill out the form. Given a proper HTTP parameters this module builds a survey form so that user can answer the given question and then stores the information in the database.</li>
<li>SurveyResultsAnalizer - module responsible for displaying the answers of respondents who had already filled out the survey.</li>
<li>SingleSurveyEditor - module which is sent to the respondent so that he/she could edit his/her answers. Behaves similarly to SurveyFiller module with the proviso that the survey form is already filled with previous user answers at the beginning. It is also impossible to change the user name.</li>
</ul>

</p>

<p>To create a new survey go to the <a href="survey-generator.links">SurveyGenerator</a> module !</p>
<p>By the time you have created following surveys: </p>

<table border="1">

<tr border="1">
<td border="1"><b>Survey name</b></td>
<td border="1"><b>Number of Respondents</b></td>
<td border="1"><b>Results</b></td>
<td border="1"><b>SurveyFiller module</b></td>
<td border="1"><b>Delete option</b></td>
</tr>

{
for (rsurvey <- query 
	{
	for (rsurvey <-- surveys) 
	[rsurvey]
	}
	)
		{
		<tr border="1">
		<td border="1">
		{
		if(getNumberOfRespondents(rsurvey.id) > 0)
		{
		<a href="{generateSurveyRespondentsLink(rsurvey.id)}">{stringToXml(rsurvey.name)}</a>
		}
		else
		{
		{stringToXml(rsurvey.name)}
		}
		}
		</td>
		<td border="1">{intToXml(getNumberOfRespondents(rsurvey.id))}</td>
		
		<td border="1">
		{
		if(getNumberOfRespondents(rsurvey.id) > 0)
		{
		<a href="{generateSurveyResultsAnalyzerLink(rsurvey.id)}">click here to see</a>
		}
		else
		{
		{stringToXml("lack of results")}
		}
		}
		
		</td>

		<td border="1"><a href="{generateSurveyFillerLink(rsurvey.id)}">click here to go</a></td>
		<td border="1"><a href="{generateSurveyDestroyerLink(rsurvey.id)}">click here to remove</a></td>
		</tr>
		}
        
}

</table>

<br></br>
<hr /> 
<em>Powered by <a href="http://groups.inf.ed.ac.uk/links/">Links</a>.</em>   

</body>				  
</html>

}

main()