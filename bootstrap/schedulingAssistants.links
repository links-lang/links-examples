open Bootstrap
open Common


var dummyEntries = [
	(active=1,departmentid=8,employeeid=226,jobtitle="Scheduling Assistant",loginid="adventure-works\\brian1",nationalidnumber="368691270",shiftid=3,sickleavehours=44,vacationhours=48), 
	(active=1,departmentid=8,employeeid=225,jobtitle="Scheduling Assistant",loginid="adventure-works\\alan0",nationalidnumber="470689086",shiftid=2,sickleavehours=43,vacationhours=47), 
	(active=1,departmentid=8,employeeid=224,jobtitle="Scheduling Assistant",loginid="adventure-works\\william0",nationalidnumber="621209647",shiftid=1,sickleavehours=42,vacationhours=45), 
	(active=0,departmentid=7,employeeid=224,jobtitle="Scheduling Assistant",loginid="adventure-works\\william0",nationalidnumber="621209647",shiftid=1,sickleavehours=42,vacationhours=45), 
	(active=1,departmentid=8,employeeid=223,jobtitle="Scheduling Assistant",loginid="adventure-works\\sairaj0",nationalidnumber="981495526",shiftid=1,sickleavehours=43,vacationhours=46)
];


fun entriesPage(entries) {

	var putback = fun (res) {
		var entries = filter(fun (entry) {not(entry.del)}, res.entries);
		var entries = map(fun (entry) {entry.entry}, entries);
		var entries = if (res.add)
			res.newentry :: entries
		else
			entries;

		entriesPage(entries)
	};

	var entryFormlet = fun (entry) {
		formlet <#>
			<div class="panel panel-default">
			<div class="panel-body">
				<div class="row">
					<div class="col-lg-3">
						<div class="input-group">
							<span class="input-group-addon" id="basic-addon1">{intToXml(entry.employeeid)}</span>
							{ Bootstrap.inputValue(entry.loginid) -> loginid}
						</div>
					</div>
					{ Bootstrap.inputIntValueCol(entry.departmentid, 1) -> departmentid }
					{ Bootstrap.inputValueCol(entry.nationalidnumber, 2) -> nationalidnumber }

					{ Bootstrap.inputIntValueCol(entry.shiftid, 1) -> shiftid }
					{ Bootstrap.inputIntValueCol(entry.active, 1) -> active }

					{ Bootstrap.inputIntValueCol(entry.vacationhours, 1) -> vacationhours}
					{ Bootstrap.inputIntValueCol(entry.sickleavehours, 1) -> sickleavehours}

					<div class="col-lg-1">
					</div>
					<div class="col-lg-1">
						{ Bootstrap.submitButtonDefaultA([], <#>
							<span class="glyphicon glyphicon-remove"></span>
							</#>) -> del }
					</div>
				</div>
			</div>
			</div>
		</#>
		yields (entry = (employeeid = entry.employeeid, departmentid = departmentid, loginid = loginid, nationalidnumber = nationalidnumber, shiftid = shiftid, active = active, jobtitle = entry.jobtitle, vacationhours = vacationhours, sickleavehours = sickleavehours), del = del)
	};

	var addFormlet = 
		formlet <#>
			<div class="panel panel-default">
				<div class="panel-heading">Add new entry</div>
				<div class="panel-body">
					<div class="row">
						{ Bootstrap.inputIntValueCol(0, 1) -> employeeid }
						{ Bootstrap.inputValueCol("", 3) -> loginid }

						{ Bootstrap.inputIntValueCol(4, 1) -> departmentid }
						{ Bootstrap.inputValueCol("", 2) -> nationalidnumber }

						{ Bootstrap.inputIntValueCol(1, 1) -> shiftid }
						{ Bootstrap.inputIntValueCol(1, 1) -> active }

						{ Bootstrap.inputIntValueCol(0, 1) -> vacationhours}
						{ Bootstrap.inputIntValueCol(0, 1) -> sickleavehours}
					</div>
				</div>
			</div>
		</#>
		yields (employeeid = employeeid, departmentid = departmentid, loginid = loginid, nationalidnumber = nationalidnumber, shiftid = shiftid, active = active, jobtitle = "Scheduling Assistant", vacationhours = vacationhours, sickleavehours = sickleavehours);


	var entryFormlets = 
		formlet <#>
			<div>
				<div class="well well-sm">
					<div class="row">
						<div class="col-lg-3">
							Login Id
						</div>
						<div class="col-lg-1">
							Dep Id
						</div>
						<div class="col-lg-2">
							National Id
						</div>
						<div class="col-lg-1">
							Shift Id
						</div>
						<div class="col-lg-1">
							Active
						</div>
						<div class="col-lg-1">
							Vacation
						</div>
						<div class="col-lg-1">
							Sickleave
						</div>					
					</div>
				</div>
				<div>
					{ formlets(map(entryFormlet, entries)) -> entries}
				</div>
				<div>
					{ addFormlet -> newentry }
				</div>
				<div>
					{ Bootstrap.submitButtonDefaultText("Add") -> add }
					{ Bootstrap.submitButtonDefaultText("Save") -> save }
				</div>
				<div>
				</div>
			</div>
		</#>
		yields (entries = entries, add = add, save = save, newentry = newentry);

	page
		<html>
			{ Bootstrap.header }
			<body>
				{ Common.navBar("/jobs/scheduling_assistants") }
				<div class="container main">
					<h1>Scheduling Assistants</h1>

					<div>
					{ entryFormlets => putback with multipart	}
					</div>
				</div>
			</body>
		</html>
}

fun mainPage(_,_) {
	var entries = dummyEntries;
	
	entriesPage(entries)
}

fun main() {
	addRoute("/", mainPage);
	addStaticRoute("/test", "test/", [("html", "text/html")]);

	Bootstrap.addRoutes();

	servePages()
}

main()
